plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.11.2'
}

def allureVersion = "2.25.0",
    selenideVersion = "7.1.0",
    fakerVersion = "1.0.2",
    junit5Version = "5.10.2"

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation(
            "com.codeborne:selenide:$selenideVersion",
            "com.github.javafaker:javafaker:$fakerVersion",
            "org.junit.jupiter:junit-jupiter:$junit5Version",
            "io.qameta.allure:allure-selenide:$allureVersion")
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    useJUnitPlatform()
    systemProperties(System.getProperties())
    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task demoqa_test(type: Test) {
    useJUnitPlatform {
        includeTags("demoqa")
    }
}